load("@rules_java//java:java_binary.bzl", "java_binary")
load("@rules_java//java:java_library.bzl", "java_library")
load("//legend-pure/legend-pure-core/legend-pure-m3-core:pure.bzl", "pure_par")
load("//tools/build_rules:pure_generation.bzl", "pure_model_generation")

java_library(
    name = "m2-dsl-graph-pure-boot",
    srcs = glob(["src/main/java/**/*.java"]),
    resources = glob(["src/main/resources/**/*"]),
    visibility = ["//visibility:public"],
    deps = [
        "//legend-pure/legend-pure-core/legend-pure-m3-core",
        "//legend-pure/legend-pure-core/legend-pure-m4",
        "@legend_maven//:org_eclipse_collections_eclipse_collections",
        "@legend_maven//:org_eclipse_collections_eclipse_collections_api",
    ],
)

java_binary(
    name = "M3CoreInstanceGenerator",
    main_class = "org.finos.legend.pure.m3.generator.bootstrap.M3CoreInstanceGenerator",
    visibility = ["//visibility:public"],
    runtime_deps = [
        ":m2-dsl-graph-pure-boot",
        "//legend-pure/legend-pure-core/legend-pure-m3-core",
    ],
)

pure_model_generation(
    name = "pure_gen",
    srcs = glob(["src/main/resources/**/*.pure"]),
    model_name = "Graph",
    source_path = "/platform_dsl_graph/graph.pure",
    tool = ":M3CoreInstanceGenerator",
    visibility = ["//visibility:public"],
)

java_library(
    name = "legend-pure-m2-dsl-graph-pure",
    srcs = [":pure_gen"],
    visibility = ["//visibility:public"],
    exports = [
        ":m2-dsl-graph-pure-boot",
        ":platform_dsl_graph_metadata_lib",
    ],
    deps = [
        ":m2-dsl-graph-pure-boot",
        "//legend-pure/legend-pure-core/legend-pure-m3-core",
        "//legend-pure/legend-pure-core/legend-pure-m4",
        "@legend_maven//:org_eclipse_collections_eclipse_collections",
        "@legend_maven//:org_eclipse_collections_eclipse_collections_api",
        ":platform_dsl_graph_metadata_lib",
    ],
)

java_library(
    name = "platform_dsl_graph_metadata_lib",
    resources = [":platform_dsl_graph_metadata"],
    resource_strip_prefix = "legend-pure/legend-pure-dsl/legend-pure-dsl-graph/legend-pure-m2-dsl-graph-pure",
    visibility = ["//visibility:public"],
)

genrule(
    name = "platform_dsl_graph_metadata",
    outs = ["metadata/specs/platform_dsl_graph.json"],
    cmd = "mkdir -p $$(dirname $@) && echo '{\"name\": \"platform_dsl_graph\", \"dependencies\": [\"platform\"]}' > $@",
    visibility = ["//visibility:public"],
)

java_binary(
    name = "PureJarGenerator",
    main_class = "org.finos.legend.pure.m3.generator.par.PureJarGenerator",
    visibility = ["//visibility:public"],
    runtime_deps = [
        ":m2-dsl-graph-pure-boot",
        "//legend-pure/legend-pure-core/legend-pure-m3-core",
    ],
)

pure_par(
    name = "pure_platform_dsl_graph_par",
    out = "pure-platform_dsl_graph.par",
    repo_name = "platform_dsl_graph",
    tool = ":PureJarGenerator",
    visibility = ["//visibility:public"],
)

java_library(
    name = "pure_platform_dsl_graph_par_lib",
    resource_strip_prefix = "legend-pure/legend-pure-dsl/legend-pure-dsl-graph/legend-pure-m2-dsl-graph-pure",
    resources = [":pure-platform_dsl_graph.par"],
    visibility = ["//visibility:public"],
)

java_binary(
    name = "compiled_metadata_generator",
    main_class = "org.finos.legend.pure.runtime.java.compiled.generation.orchestrator.JavaCodeGeneration",
    runtime_deps = [
        "//legend-pure/legend-pure-runtime/legend-pure-runtime-java-engine-compiled",
        ":legend-pure-m2-dsl-graph-pure",
    ],
    visibility = ["//visibility:public"],
)

genrule(
    name = "platform_dsl_graph_compiled_graph",
    srcs = glob(["src/main/resources/**/*"]),
    outs = ["platform_dsl_graph_compiled_metadata.jar"],
    tools = [":compiled_metadata_generator"],
    cmd = """
    mkdir -p $(@D)/graph_gen_out
    mkdir -p $(@D)/graph_classes_out
    export JAVA_OPTS="-Dpure.usePar=false"
    $(location :compiled_metadata_generator) \
        platform_dsl_graph \
        $(@D)/graph_classes_out \
        $(@D)/graph_gen_out

    jar cf $@ -C $(@D)/graph_classes_out metadata
    """,
    visibility = ["//visibility:public"],
)

java_import(
    name = "platform_dsl_graph_compiled_graph_import",
    jars = [":platform_dsl_graph_compiled_metadata.jar"],
    visibility = ["//visibility:public"],
)
